-	shop	牢鸽#shop::prison_pigeon_shop	-1,522:100,536:100,608:300,525:500,678:1000,7139:200,14517:1000,14518:1000,14519:1000,14520:1000,12645:1000,12020:1000,12114:1000,12115:1000,12116:1000,12117:1000,12202:10000,12203:10000,12204:10000,12205:10000,12206:10000,12207:10000,12214:1000,645:1200,656:2200,657:4500,6484:10000

prontera,165,107,4	script	牢鸽	801,{
function get_station_user;
function set_station_user;

	function get_station_user {
		.@idx = getarg(0);
		switch (.@idx) {
		case 1:
			return $@overtime_work_station_1;
		case 2:
			return $@overtime_work_station_2;
		case 3:
			return $@overtime_work_station_3;
		case 4:
			return $@overtime_work_station_4;
		}
	}

	function set_station_user {
		.@idx = getarg(0);
		.@user = getarg(1);
		switch (.@idx) {
		case 1:
			set $@overtime_work_station_1, .@user;
			break;
		case 2:
			set $@overtime_work_station_2, .@user;
			break;
		case 3:
			set $@overtime_work_station_3, .@user;
			break;
		case 4:
			set $@overtime_work_station_4, .@user;
			break;
		}
	}

	mes "[牢鸽]";
	mes "组撒？";
	next;
	switch (select("牢:鸽:古希腊掌管加班的神")) {
	case 1:
		mes "[牢鸽]";
		mes "你别慌~";
		next;
		switch (select("暗搓搓:越狱")) {
		case 1:
			close2;
			callshop "prison_pigeon_shop",1;
			end;
		case 2:
			set .@eqi_ref_max_lv,9;
			for (set .@eqi_idx,EQI_ACC_L; .@eqi_idx<EQI_MAX; set .@eqi_idx,.@eqi_idx+1) {
				if (getequipisequiped(.@eqi_idx) && getequipisenableref(.@eqi_idx)) {
					for (set .@eqi_ref_lv,getequiprefinerycnt(.@eqi_idx); .@eqi_ref_lv<.@eqi_ref_max_lv; set .@eqi_ref_lv,.@eqi_ref_lv+1) {
						successrefitem .@eqi_idx;
					}
				}
			}
			specialeffect2 EF_REFINEFAIL;
			mes "[牢鸽]";
			mes "溜了溜了~";
			close;
		}
	case 2:
		mes "[牢鸽]";
		mes "咯咯咯";
		close;
	case 3:
		for (set .@idx,1; .@idx < 5; set .@idx,.@idx+1) {
			.@user = get_station_user(.@idx);
			if (.@user == 0) {
				close2;
				set_station_user(.@idx, getcharid(0));
				warp "06guild_0"+.@idx,51,51;
				donpcevent "加班的牢鸽#"+ .@idx +"::OnEnterMap";
				end;
			}
		}

		next;
		mes "[牢鸽]";
		for (set .@idx,1; .@idx < 5; set .@idx,.@idx+1) {
			mes "^FF0000"+strcharinfo(0, get_station_user(.@idx))+"^000000";
		}
		mes "这四个逼正在加班，快去看看哪个是工贼！";
		close;
	}
}

-	script	加班的牢鸽	-1,{
function clean_map;

	function clean_map {
		.@idx = getarg(0);
		stopnpctimer;
		switch (.@idx) {
		case 1:
			set $@overtime_work_station_1, 0;
			break;
		case 2:
			set $@overtime_work_station_2, 0;
			break;
		case 3:
			set $@overtime_work_station_3, 0;
			break;
		case 4:
			set $@overtime_work_station_4, 0;
			break;
		}
		killmonsterall "06guild_0"+.@idx;
	}

	mes "[加班的牢鸽]";
	mes "开始加班？";
	next;
	if (select("无情的混工时机器:溜了溜了") == 1) {
		doevent "加班::OnStart";
		end;
	} else {
		close2;
		clean_map(atoi(strnpcinfo(2)));
		warp "prontera",161,107;
		end;
	}

OnEnterMap:
	.@idx = atoi(strnpcinfo(2));
	killmonsterall "06guild_0"+.@idx;
	initnpctimer;
	end;

OnTimer5000:
	.@idx = atoi(strnpcinfo(2));
	if (getmapusers("06guild_0"+.@idx) == 0) {
		clean_map(.@idx);
	} else {
		initnpctimer;
	}
	end;
}

-	script	加班	-1,{
	end;

OnStart:
	getmapxy(.@map$, .@x, .@y, BL_PC);
	killmonsterall .@map$;
	monster .@map$,53,53,"牢鸽的工位",2411,1,"加班::OnMobDead";
	getareagid(.@mob_gids, 0, BL_MOB, .@map$);
	.@mob_gid = .@mob_gids[0];
	.@account_id = getcharid(3);
	detachrid;
	unitattack .@account_id, .@mob_gid, 1;
	attachrid(.@account_id);
	end;

OnMobDead:
	dispbottom "摸鱼大成功";
	specialeffect2 EF_COIN;
	Zeny += 1000000;
	callsub OnStart;
	end;
}

06guild_01,49,49,4	duplicate(加班的牢鸽)	加班的牢鸽#1	849
06guild_02,49,49,4	duplicate(加班的牢鸽)	加班的牢鸽#2	849
06guild_03,49,49,4	duplicate(加班的牢鸽)	加班的牢鸽#3	849
06guild_04,49,49,4	duplicate(加班的牢鸽)	加班的牢鸽#4	849

06guild_01	mapflag	nowarpto
06guild_02	mapflag	nowarpto
06guild_03	mapflag	nowarpto
06guild_04	mapflag	nowarpto
06guild_01	mapflag	nomemo
06guild_02	mapflag	nomemo
06guild_03	mapflag	nomemo
06guild_04	mapflag	nomemo
06guild_01	mapflag	noteleport
06guild_02	mapflag	noteleport
06guild_03	mapflag	noteleport
06guild_04	mapflag	noteleport
06guild_01	mapflag	nosave	SavePoint
06guild_02	mapflag	nosave	SavePoint
06guild_03	mapflag	nosave	SavePoint
06guild_04	mapflag	nosave	SavePoint
06guild_01	mapflag	nopenalty
06guild_02	mapflag	nopenalty
06guild_03	mapflag	nopenalty
06guild_04	mapflag	nopenalty
06guild_01	mapflag	restricted	4096
06guild_02	mapflag	restricted	4096
06guild_03	mapflag	restricted	4096
06guild_04	mapflag	restricted	4096





prontera,146,107,6	script	牢二	878,{
	mes "[牢二]";
	mes "怎么形容我最贴切？";
	next;
	switch (select("持久:专一:持久专一:古希腊掌管氪金的神")) {
	case 1:
		mes "[牢二]";
		mes "讨厌~";
		sc_start SC_FREEZE,10000,0;
		sc_start SC_POISON,10000,0;
		sc_start SC_CURSE,10000,0;
		sc_start SC_SILENCE,10000,0;
		close;
	case 2:
		mes "[牢二]";
		mes "恶心心~";
		sc_start SC_STUN,10000,0;
		sc_start SC_CONFUSION,10000,0;
		sc_start SC_BLIND,10000,0;
		sc_start SC_BLEEDING,10000,0;
		close;
	case 3:
		if (prison_two_tick > gettimetick(2)) {
			mes "[牢二]";
			mes "你有点粘人了~";
			specialeffect2 EF_HIT1;
			percentheal -10,-10;
			close;
		}

		mes "[牢二]";
		mes "赏你了~";
		specialeffect2 EF_COIN;
		Zeny += 10000;
		prison_two_tick = gettimetick(2) + 60;
		close;
	case 4:
		mes "[牢二]";
		mes "要充几个648？";
		next;
		input .@input_count;
		if (.@input_count * 648 > Zeny) {
			mes "[牢二]";
			mes "买不起别玩~";
			sc_start SC_CURSE,60000,0;
			specialeffect2 EF_HIT2;
			percentheal -99,-99;
		} else {
			mes "[牢二]";
			mes "起飞~";
			Zeny -= (.@input_count * 648);
			#CASHPOINTS += .@input_count;
		}
		close;
	}
}
